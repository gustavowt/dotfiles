function prdesc
    echo "ğŸ” Checking if inside a git repository..."
    if not git rev-parse --show-toplevel >/dev/null 2>&1
        echo "âŒ Not in a git repo" >&2
        return 1
    end
    echo "âœ… Git repo detected"

    # detect PR template
    set -l repo_root (git rev-parse --show-toplevel)
    set -l template_file ""
    echo "ğŸ“‘ Looking for PR template..."
    for p in ".github/pull_request_template.md" "PULL_REQUEST_TEMPLATE.md"
        if test -f "$repo_root/$p"
            set template_file "$repo_root/$p"
            echo "âœ… Found PR template at $p"
            break
        end
    end
    if test -z "$template_file"
        echo "âš ï¸  No PR template found, will use default headings"
    end

    # find default branch
    echo "ğŸ” Detecting default branch..."
    set -l default_remote_head (git symbolic-ref --quiet --short refs/remotes/origin/HEAD 2>/dev/null)
    set -l default_branch (string replace -r '^origin/' '' -- "$default_remote_head")
    if test -z "$default_branch"
        if git show-ref --verify --quiet refs/remotes/origin/main
            set default_branch "main"
        else if git show-ref --verify --quiet refs/remotes/origin/master
            set default_branch "master"
        else
            echo "âŒ Could not detect default branch" >&2
            return 1
        end
    end
    echo "âœ… Default branch detected: $default_branch"

    # diff
    echo "ğŸ”„ Computing diff against $default_branch..."
    set -l base (git merge-base HEAD "origin/$default_branch" 2>/dev/null)
    if test -z "$base"
        echo "âŒ Could not determine merge-base with origin/$default_branch" >&2
        return 1
    end

    set -l diff (git diff --unified=0 --no-color $base...HEAD)
    if test -z "$diff"
        echo "â„¹ï¸  No changes vs $default_branch" >&2
        return 1
    end
    echo "âœ… Diff collected"

    # branch
    set -l current_branch (git rev-parse --abbrev-ref HEAD)
    echo "ğŸ“Œ Current branch: $current_branch"

    # check if PR already exists
    echo "ğŸ” Checking if PR already exists..."
    if gh pr view >/dev/null 2>&1
        echo "âš ï¸  PR already exists for this branch:"
        gh pr view --web
        return 0
    end
    echo "âœ… No existing PR found"

    # extract JIRA ticket from branch name
    echo "ğŸ« Extracting JIRA ticket from branch name..."
    set -l jira_ticket (string match -r '^[A-Z]+-[0-9]+' $current_branch)
    if test -z "$jira_ticket"
        echo "âš ï¸  Warning: Could not extract JIRA ticket from branch name"
    else
        echo "âœ… JIRA ticket: $jira_ticket"
    end

    # build prompt
    echo "ğŸ“ Building Claude prompt..."
    set -l prompt "You write precise Pull Request titles and descriptions from diffs.

Rules:
- First line MUST be the PR title in format: [$jira_ticket] - <concise title>
- Follow with a blank line
- Then write the PR body using the repo's PR template headings exactly if provided.
- Be direct. Short sentences and bullet points.
- No filler words, fluff, or marketing language.
- Only describe what the diff changes and why it's needed.
- If a section doesn't apply, write 'N/A'.
- Keep the body under 300 words.
- Replace JIRA references with: https://doximity.atlassian.net/browse/$jira_ticket
- MAKE sure dont change the layout of the template if provided. Do not add any extra style or remove anything

Repository default branch: $default_branch
Current branch: $current_branch
JIRA ticket: $jira_ticket
"

    if test -n "$template_file"
        set prompt "$prompt\nPR Template:\n"(cat "$template_file")"\n"
    else
        set prompt "$prompt\n(No template found. Use sections: Summary, Changes, Motivation, Risks, Test Plan, Rollout, Checklist.)\n"
    end

    set prompt "$prompt\nGit Diff (unified=0):\n```diff\n$diff\n```"

    # call Claude CLI and save output
    echo "ğŸ¤– Sending prompt to Claude..."
    set -l temp_file (mktemp)
    claude -p "$prompt" > "$temp_file"

    if test $status -ne 0
        echo "âŒ Claude CLI failed" >&2
        rm -f "$temp_file"
        return 1
    end
    echo "âœ… Claude response received"

    # extract title (first line) and body (rest)
    set -l pr_title (head -n 1 "$temp_file")
    set -l pr_body (tail -n +3 "$temp_file" | string collect)  # skip title and blank line

    echo "ğŸ“‹ Creating PR with gh..."
    echo "Title: $pr_title"

    # create a body file for gh
    set -l body_file (mktemp)
    echo "$pr_body" > "$body_file"

    # create PR with editor for final edits
    gh pr create --title "$pr_title" --body-file "$body_file" --editor
    set -l gh_status $status

    # cleanup
    rm -f "$temp_file" "$body_file"

    if test $gh_status -eq 0
        echo "âœ… PR created successfully!"

        # Ask for code review
        echo ""
        read -P "Would you like to request a code review? (y/n): " -l response
        if test "$response" = "y" -o "$response" = "yes"
            echo "ğŸ¤– Requesting code review..."
            gh pr comment --body "doxbot cr"
            if test $status -eq 0
                echo "âœ… Code review requested!"
            else
                echo "âŒ Failed to request code review" >&2
            end
        end
    else
        echo "âŒ Failed to create PR" >&2
        return 1
    end
end
